extends "common/base.pkl"
import "common/common.pkl"


hidden extraBuildSteps: Listing<Step>
hidden modules: List<String>
hidden sonatypeEnabled = true

name = "Build Library"

on = new On {
    push = new Push {
        branches = new Listing {
            "main"
            "develop"
            "release/**"
        }
    }
    pull_request = new PullRequest {
        branches = new Listing {
            "main"
            "develop"
        }
    }
}

permissions = new Mapping {
    ["contents"] = "write"
}

jobs = new Mapping {
    ["version"] = common.version(0)
    ["build"] = new Job {
        strategy = new JobStrategy {
            matrix = new Mapping {
                ["module"] = modules
            }
        }
        needs = new Listing {
            "version"
        }
        steps = new Listing {
            common.checkout
            common.setupJdk
            ...extraBuildSteps
            ...common.gradle(
                "Build", 
                "${{ matrix.module }}:build",
                new Mapping {
                    ["ORG_GRADLE_PROJECT_version"] = "${{ needs.version.outputs.version }}"
                }
            )
            new CommandStep {
                name = "Rename AAR"
                run = "mv ./${{ matrix.module }}/build/outputs/aar/${{ matrix.module }}-${{ needs.version.outputs.version }}.aar ./${{ matrix.module }}.aar"
            }
            new ActionStep {
                uses = "actions/upload-artifact@v4"
                with = new Mapping {
                    ["name"] = "${{ matrix.module }}-aar"
                    ["path"] = "./${{ matrix.module }}.aar"
                }
            }
        }
    }
    ["release"] = new Job {
        needs = new Listing {
            "version"
        }
        `if` = "github.event_name != 'pull_request'"
        permissions = new Mapping {
            ["contents"] = "write"
        }
        outputs = new Mapping {
            ["upload_url"] = "${{ steps.create_release.outputs.upload_url }}"
        }
        steps = new Listing {
            common.checkout
            ...common.create_release
        }
    }
    ["publish-release"] = new Job {
        strategy = new JobStrategy {
            matrix = new Mapping {
                ["module"] = modules
            }
        }
        needs = new Listing {
            "version"
            "release"
            "build"
        }
        steps = new Listing {
            new ActionStep {
                uses = "actions/download-artifact@v4"
                with = new Mapping {
                    ["name"] = "${{ matrix.module }}-aar"
                    ["path"] = "./"
                }
            }
            new ActionStep {
                name = "Upload Artifact to Release"
                uses = "shogo82148/actions-upload-release-asset@v1"
                env = new Mapping {
                    ["GITHUB_TOKEN"] = "${{ secrets.GITHUB_TOKEN }}"
                }
                with = new Mapping {
                    ["upload_url"] = "${{ needs.release.outputs.upload_url }}"
                    ["asset_path"] = "./${{ matrix.module }}.aar"
                    ["asset_name"] = "${{ matrix.module }}-v${{ needs.version.outputs.version }}.aar"
                    ["asset_content_type"] = "application/zip"
                }
            }
        }  
    }
    ["publish-sonatype"] = if(sonatypeEnabled) new Job {
        strategy = new JobStrategy {
            matrix = new Mapping {
                ["module"] = modules
            }
        }
        permissions = new Mapping {
            ["contents"] = "read"
        }
        needs = new Listing {
            "version"
        }
        `if` = "github.ref == 'refs/heads/main'"
        steps = new Listing {
            common.checkout
            common.setupJdk
            ...common.gradle(
                "Build", 
                """
                -Pversion=${{ needs.version.outputs.version }} 
                -Ppom.url="https://github.com/${{ github.repository }}" 
                -Ppom.scm.connection="scm:git:git://github.com/${{ github.repository }}" 
                -Ppom.scm.developerConnection="scm:git:ssh://github.com/${{ github.repository }}" 
                -Ppom.scm.url="https://github.com/${{ github.repository }}" 
                ${{ matrix.module }}:publishToCentralPortal --no-daemon
                """,
                new Mapping {
                    ["ORG_GRADLE_PROJECT_sonatypePassword"] = "${{ secrets.SONATYPE_PASSWORD }}"
                    ["ORG_GRADLE_PROJECT_sonatypeUsername"] = "${{ secrets.SONATYPE_USERNAME }}"
                    ["ORG_GRADLE_PROJECT_signingKey"] = "${{ secrets.PGP_SECRET }}"
                    ["ORG_GRADLE_PROJECT_signingPassword"] = "${{ secrets.PGP_PASSPHRASE }}"
                }
            )
        }  
    } else null
}.toMap().filter((k,v) -> v != null).toMapping()