open module support
extends "./base.pkl"
import "./common.pkl"

class BuildDescriptor {
    appModule: String = "app"
    variant: String
    flavor: String?
    release: Boolean = variant == "release"

    @Deprecated { message = "Replace with buildName()" }
    function full() = buildName(false)

    function buildName(alwaysIncludeDebug: Boolean) =
        let (variantName = if (variant == "debug" && !alwaysIncludeDebug) "" else variant.capitalize())
        let (flavorName = if (flavor == null) "" else flavor.capitalize())
        "\(flavorName)\(variantName)"
    function path() = if (flavor == null) variant else "\(flavor)/\(variant)"
    function name() = if (flavor == null) variant else "\(flavor)-\(variant)"
}

hidden const RELEASE_BUILD_DESCRIPTOR = new BuildDescriptor {
    appModule = "app"
    variant = "release"
}
hidden const DEBUG_BUILD_DESCRIPTOR = new BuildDescriptor {
    appModule = "app"
    variant = "debug"
}

typealias ReleaseTrack = "internal" | "alpha" | "beta" | "production"

class AppReleaseDescriptor {
    appModule: String = "app"
    flavor: String?
    packageName: String
    track: ReleaseTrack = "internal"
}