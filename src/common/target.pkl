import "base-import.pkl" as base
import "common.pkl"

class Artifact {
  key: String
  name: String
  mimeType: String
}

class KMPTarget {
  buildCommand: String
  filesystemLocation: String
  artifactName: String
  artifactKey: String
  needsZip: Boolean = false
  artifactMimeType: String = "application/zip"

  function toArtifact(): Artifact = new Artifact {
    key = artifactKey
    name = "\(artifactName)\(if(needsZip) ".zip" else "")"
    mimeType = artifactMimeType
  }
}

iosTargets = new Listing {
  "Arm64"
  "SimulatorArm64"
  "X64"
}

function buildIosKmpTarget(moduleName: String, moduleBuildPath: String, target: String) = new KMPTarget {
  buildCommand = ":\(moduleName):linkPodReleaseFrameworkIos\(target.capitalize())"
  filesystemLocation = "\(moduleBuildPath)/bin/ios\(target.capitalize())/podReleaseFramework/\(moduleName).framework"
  artifactName = "\(moduleName)-ios\(target.capitalize()).framework"
  artifactKey = "\(moduleName)-ios\(target.capitalize())"
  needsZip = true
}

function buildAndroidKmpTarget(moduleName: String, moduleBuildPath: String) = new KMPTarget {
  buildCommand = ":\(moduleName):assembleRelease"
  filesystemLocation = "\(moduleBuildPath)/outputs/aar/\(moduleName)-release.aar"
  artifactName = "\(moduleName)-android.aar"
  artifactKey = "\(moduleName)-aar"
}

function buildJvmKmpTarget(moduleName: String, moduleBuildPath: String) = new KMPTarget {
  buildCommand = ":\(moduleName):assembleJvm"
  filesystemLocation = "./(\(moduleBuildPath)/libs/\(moduleName)-jvm.jar"
  artifactName = "\(moduleName)-java.jvm"
  artifactKey = "\(moduleName)-jvm"
}

function buildJsKmpTarget(moduleName: String, moduleBuildPath: String) = new KMPTarget {
  buildCommand = ":\(moduleName):assembleJs"
  filesystemLocation = "./(\(moduleBuildPath)/distributions/\(moduleName).js"
  artifactName = "\(moduleName)-javascript.js"
  artifactKey = "\(moduleName)-js"
  artifactMimeType = "text/javascript"
}

function toJobStrategy(targets: Listing<KMPTarget>): base.JobStrategy = new base.JobStrategy {
  matrix = new Mapping {
    ["configuration"] = new Listing {
      for (target in targets) {
        target.toMap()
      }
    }
  }
}

function buildTargetJob(targets: Listing<KMPTarget>, runPlatform: String) = new base.Job {
  `runs-on` = runPlatform
  strategy = toJobStrategy(targets)
  needs = new Listing {
    "version"
  }
  steps = new Listing {
    common.checkout
    common.setupJdk
    ...common.gradle(
      "Build",
      "${{ matrix.configuration.buildCommand }}",
      new Mapping {
        ["ORG_GRADLE_PROJECT_version"] = "${{ needs.version.outputs.version }}"
      }
    )
    new base.CommandStep {
      name = "Rename Artifact"
      run = "mv ${{ matrix.configuration.filesystemLocation }} ./${{ matrix.configuration.artifactName }}"
    }
    new base.CommandStep {
      name = "Zip if needed"
      `if` = "${{ matrix.configuration.needsZip }}"
      run = "zip -r ${{ matrix.configuration.artifactName }}.zip ./${{ matrix.configuration.artifactName }}"
    }
    new base.ActionStep {
      uses = "actions/upload-artifact@v4"
      with = new Mapping {
        ["name"] = "${{ matrix.configuration.artifactKey }}"
        ["path"] = "./${{ matrix.configuration.artifactName }}${{ matrix.configuration.needsZip && '.zip' || '' }}"
      }
    }
  }
}