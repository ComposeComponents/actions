open module buildKmpLib
extends "common/base.pkl"
import "common/common.pkl"
import "common/target.pkl" as target

hidden extraBuildSteps: Listing<Step>
hidden modules: List<String>
hidden sonatypeEnabled = true
hidden sonatypeAutomaticRelease = false

local specializationParameters = new target.SpecializationParameters {
    version = "${{ needs.version.outputs.version }}"
}

local iosTargets = new Listing {
    "arm64"
    "simulatorArm64"
    "x64"
}

hidden targets = new Listing {
    for (m in modules) {
        for (i in iosTargets) {
            target.buildIosMultiplatformTarget(m, i)
        }
        target.buildAndroidMultiplatformTarget(m)
    }
}

name = "Build Library"

on = new On {
    push = new Push {
        branches = new Listing {
            "main"
            "develop"
            "release/**"
        }
    }
    pull_request = new PullRequest {
        branches = new Listing {
            "main"
            "develop"
        }
    }
}

permissions = new Mapping {
    ["contents"] = "write"
}

jobs = new Mapping {
    ["version"] = common.version(0)
    ["build"] = target.buildTargetJob(
        targets,
        specializationParameters,
        extraBuildSteps
    )
    ["release"] = new Job {
        needs = new Listing {
            "version"
        }
        `if` = "github.event_name != 'pull_request'"
        permissions = new Mapping {
            ["contents"] = "write"
        }
        outputs = new Mapping {
            ["upload_url"] = "${{ steps.create_release.outputs.upload_url }}"
        }
        steps = new Listing {
            common.checkout
            ...common.create_release
        }
    }
    ["publish-release"] = new Job {
        strategy = new JobStrategy {
            matrix = new Mapping {
                ["artifact"] = new Listing {
                    for (t in targets) {
                        for (output in t.outputs) {
                            target.Artifact(
                                output,
                                specializationParameters
                            )
                        }
                    }
                }
            }
        }
        needs = new Listing {
            "version"
            "release"
            "build"
        }
        steps = new Listing {
            new ActionStep {
                uses = "actions/download-artifact@v4"
                with = new Mapping {
                    ["name"] = "${{ matrix.artifact.key }}"
                    ["path"] = "./"
                }
            }
            new CommandStep {
                name = "Add version to file name"
                run = """
                    file="${{ matrix.artifact.name }}"
                    echo "NEW_NAME=${file%%.*}-v${{ needs.version.outputs.version }}.${file#*.}" >> $GITHUB_ENV
                    """
            }
            new ActionStep {
                name = "Upload Artifact to Release"
                uses = "shogo82148/actions-upload-release-asset@v1"
                env = new Mapping {
                    ["GITHUB_TOKEN"] = "${{ secrets.GITHUB_TOKEN }}"
                }
                with = new Mapping {
                    ["upload_url"] = "${{ needs.release.outputs.upload_url }}"
                    ["asset_path"] = "./${{ matrix.artifact.name }}"
                    ["asset_name"] = "${{ env.NEW_NAME }}"
                    ["asset_content_type"] = "${{ matrix.artifact.mimeType }}"
                }
            }
        }
    }
    ["publish-sonatype"] = if(sonatypeEnabled) new Job {
        // Since everything builds on macOS, just use macOS
        `runs-on` = "macos-latest"
        strategy = new JobStrategy {
            matrix = new Mapping {
                ["module"] = modules
            }
        }
        permissions = new Mapping {
            ["contents"] = "read"
        }
        needs = new Listing {
            "version"
        }
        `if` = "github.ref == 'refs/heads/main'"
        steps = new Listing {
            common.checkout
            common.setupJdk
            ...common.gradle(
                "Build",
                """
                -Pversion=${{ needs.version.outputs.version }}
                -Ppom.url="https://github.com/${{ github.repository }}"
                -Ppom.scm.connection="scm:git:git://github.com/${{ github.repository }}"
                -Ppom.scm.developerConnection="scm:git:ssh://github.com/${{ github.repository }}"
                -Ppom.scm.url="https://github.com/${{ github.repository }}"
                ${{ matrix.module }}:publish\(if (sonatypeAutomaticRelease) "AndRelease" else "")ToMavenCentral --no-configuration-cache
                """,
                new Mapping {
                    ["ORG_GRADLE_PROJECT_mavenCentralPassword"] = "${{ secrets.SONATYPE_PASSWORD }}"
                    ["ORG_GRADLE_PROJECT_mavenCentralUsername"] = "${{ secrets.SONATYPE_USERNAME }}"
                    ["ORG_GRADLE_PROJECT_signingInMemoryKey"] = "${{ secrets.PGP_SECRET_KMP }}"
                    ["ORG_GRADLE_PROJECT_signingInMemoryKeyPassword"] = "${{ secrets.PGP_PASSPHRASE }}"
                }
            )
        }
    } else null
}.toMap().filter((k,v) -> v != null).toMapping()